class_name Interactible
extends Area2D

## Объект, с которым можно взаимодействовать.
##
## По умолчанию не несёт в себе формы столкновения, добавьте её вручную дочерним
## [CollisionShape2D].

## Издаётся после успешного взаимодействия. В [param who] хранится игрок,
## который провзаимодействовал.
signal interacted(who: Player)

## Текст, появляющийся над стрелкой взаимодействия.
@export_multiline var text: String
## Время, которое должна быть зажата кнопка для взаимодействия.
@export var hold_interaction_time := -1.0

var _players: Array[Player]
var _local_player: Player
var _hold_interaction_timers: Dictionary[Player, float]
var _arrow_tween: Tween
var _interact_tween: Tween

@onready var _label: Label = $Visual/Label
@onready var _arrow: Sprite2D = $Visual/Arrow
@onready var _hold_timer_progress: TextureProgressBar = $Visual/Label/HoldTimerProgress
@onready var _visual: Node2D = $Visual


func _ready() -> void:
	_visual.hide()
	set_text(text)


func _process(delta: float) -> void:
	if _hold_interaction_timers.is_empty():
		return
	
	for player: Player in _hold_interaction_timers.keys():
		_hold_interaction_timers[player] -= delta
		if player == _local_player:
			_hold_timer_progress.value = 1.0 - _hold_interaction_timers[player] \
					/ hold_interaction_time
			_hold_timer_progress.self_modulate = \
					Color.WHITE.lerp(Color.GREEN, _hold_timer_progress.value)
		
		if _hold_interaction_timers[player] <= 0.0:
			_interact(player)
			_hold_interaction_timers.erase(player)
			if player == _local_player:
				_reset_hold_interaction()


## Задаёт текст над стрелкой взаимодействия.
func set_text(new_text: String) -> void:
	if hold_interaction_time > 0.0:
		new_text = "(удерживай)\n" + new_text
	_label.text = new_text


func _interact(player: Player) -> void:
	interacted.emit(player)
	if player != _local_player:
		return
	
	if is_instance_valid(_interact_tween):
		_interact_tween.kill()
	_interact_tween = create_tween()
	_interact_tween.tween_property(_arrow, ^":self_modulate", Color.WHITE, 0.5).from(Color.GREEN)


func _reset_hold_interaction() -> void:
	_hold_timer_progress.value = 0.0
	_hold_timer_progress.self_modulate = Color.WHITE


## Метод для переопределения. Если он возвращает [code]false[/code], игрок не может
## провзаимодействовать с этим ообъектом.
func _can_player_interact(_player: Player) -> bool:
	return true


func _on_player_input_interaction_started(player: Player) -> void:
	if hold_interaction_time < 0.0:
		_interact(player)
		return
	_hold_interaction_timers[player] = hold_interaction_time


func _on_player_input_interaction_ended(player: Player) -> void:
	if hold_interaction_time < 0.0:
		return
	if player == _local_player:
		_reset_hold_interaction()
	_hold_interaction_timers.erase(player)


func _on_body_entered(body: Node2D) -> void:
	var player := body as Player
	if not player:
		return
	if not _can_player_interact(player):
		return
	
	player.player_input.interaction_started.connect(
			_on_player_input_interaction_started.bind(player))
	player.player_input.interaction_ended.connect(
			_on_player_input_interaction_ended.bind(player))
	if player.is_local():
		player.player_input.add_interactible()
		_visual.show()
		_arrow_tween = create_tween()
		_arrow.position = Vector2.UP * 24
		_arrow_tween.tween_property(_arrow, ^":position", Vector2.UP * 16, 1.0)
		_arrow_tween.tween_property(_arrow, ^":position", Vector2.UP * 24, 1.0)
		_arrow_tween.set_loops(0)
		_local_player = player
	
	_players.append(player)


func _on_body_exited(body: Node2D) -> void:
	var player := body as Player
	if not player:
		return
	if not player in _players:
		return
	
	player.player_input.interaction_started.disconnect(
			_on_player_input_interaction_started)
	player.player_input.interaction_ended.disconnect(
			_on_player_input_interaction_ended)
	if player == _local_player:
		player.player_input.remove_interactible()
		_visual.hide()
		_arrow_tween.kill()
		_local_player = null
		_reset_hold_interaction()
	
	_hold_interaction_timers.erase(player)
	_players.erase(player)
