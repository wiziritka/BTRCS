class_name Teamfight
extends Event

## Событие "Командный бой".

## Длительность матча.
@export var match_time: int = 180
## Время, через которое возвращаются павшие игроки.
@export var comeback_time: int = 3

var red_kills: int = 0
var blue_kills: int = 0
var _spawn_counter_red: int = 0
var _spawn_counter_blue: int = 0
var _time_remained: int

@onready var _spawn_points_red: Array[Node] = $Map/SpawnPoints0.get_children()
@onready var _spawn_points_blue: Array[Node] = $Map/SpawnPoints1.get_children()
@onready var _teamfight_ui: TeamfightUI = $UI


func _initialize() -> void:
	_spawn_points_blue.shuffle()
	_spawn_points_red.shuffle()
	
	_teamfight_ui.set_time(match_time)
	_time_remained = match_time
	if multiplayer.is_server():
		_spawn_counter_red = randi() % 5
		_spawn_counter_blue = randi() % 5


func _finish_setup() -> void:
	_update_kills.rpc(red_kills, blue_kills)


func _finish_start() -> void:
	if multiplayer.is_server():
		if not (players_teams.find_key(0) and players_teams.find_key(1)):
			_time_remained = 1
		($MatchTimer as Timer).start()


func _make_teams() -> void:
	var places: Array[int]
	places.append(floori(players_names.size() / 2.0))
	places.append(floori(players_names.size() / 2.0))
	if places[0] + places[1] != players_names.size():
		places[randi() % 2] += 1
	
	var ids: Array[int]
	ids.assign(players_names.keys())
	ids.shuffle()
	for id: int in ids:
		if id in players_teams:
			places[players_teams[id]] -= 1
			if places[players_teams[id]] < 0:
				# где-то ошиблись, вернём к нулю и вычтем из другого
				places[players_teams[id]] = 0
				places[1 - players_teams[id]] = 0
			continue
		if places[0] > 0:
			players_teams[id] = 0
			places[0] -= 1
		elif places[1] > 0:
			players_teams[id] = 1
			places[1] -= 1
		else:
			# по идее такого быть не должно, запихаем в красную
			players_teams[id] = 0


func _get_spawn_point(id: int) -> Vector2:
	var pos: Vector2
	if players_teams[id] == 0:
		pos = (_spawn_points_red[_spawn_counter_red % 5] as Node2D).global_position
		_spawn_counter_red += 1
	else:
		pos = (_spawn_points_blue[_spawn_counter_blue % 5] as Node2D).global_position
		_spawn_counter_blue += 1
	return pos


func _player_killed(_by: int, player: Player) -> void:
	if players_teams[player.id] == 0:
		blue_kills += 1
	else:
		red_kills += 1
	_update_kills.rpc(red_kills, blue_kills)
	_respawn_player(player.id)


func _player_disconnected(_who: int) -> void:
	if _time_remained <= 0:
		return
	# Недостаточно участников команд
	if not (players_teams.find_key(0) and players_teams.find_key(1)):
		_time_remained = 1


@rpc("unreliable_ordered", "call_local", "authority", 3)
func _update_time(remained: int) -> void:
	_teamfight_ui.set_time(remained)


@rpc("reliable", "call_local", "authority", 3)
func _update_kills(red: int, blue: int) -> void:
	if multiplayer.get_remote_sender_id() != MultiplayerPeer.TARGET_PEER_SERVER:
		push_error("This method must be called only by server.")
		return
	red_kills = red
	blue_kills = blue
	_teamfight_ui.set_kills(red_kills, blue_kills)
	print_verbose("Current score: %d - %d." % [red_kills, blue_kills])


@rpc("reliable", "call_local", "authority", 3)
func _show_winner(team: int) -> void:
	if multiplayer.get_remote_sender_id() != MultiplayerPeer.TARGET_PEER_SERVER:
		push_error("This method must be called only by server.")
		return
	end_event(team == local_team)
	_teamfight_ui.show_winner(team)
	print_verbose("Team won: %d." % team)


func _respawn_player(id: int) -> void:
	await get_tree().create_timer(comeback_time, false).timeout
	if _time_remained > 0 and id in players_names:
		spawn_player(id)


func _end_event() -> void:
	if not players_teams.find_key(0): # Нет красных больше
		_show_winner.rpc(1)
	elif not players_teams.find_key(1): # Нет синих больше
		_show_winner.rpc(0)
	elif red_kills > blue_kills:
		_show_winner.rpc(0)
	elif blue_kills > red_kills:
		_show_winner.rpc(1)
	else:
		_show_winner.rpc(-1)
	freeze_players.rpc()
	await get_tree().create_timer(6.5).timeout
	cleanup()
	await get_tree().create_timer(0.5).timeout
	end.rpc()


func _on_local_player_died() -> void:
	_teamfight_ui.show_comeback(comeback_time)


func _on_local_player_created(player: Player) -> void:
	player.died.connect(_on_local_player_died)


func _on_match_timer_timeout() -> void:
	_time_remained -= 1
	_update_time.rpc(_time_remained)
	if _time_remained <= 0:
		($MatchTimer as Timer).stop()
		_end_event()
